subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'java'

    // 全ソースがUTF-8であることを指定する、
    // この記述であればsourceSetが増えても対応可能。
    def defaultEncoding = 'UTF-8'
    tasks.withType(JavaCompile) {
        options.encoding = defaultEncoding
    }

    // コンパイル対象バージョンの指定
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // version指定
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    // 全てのprojectで設定される依存性
    dependencies {
        compile 'org.projectlombok:lombok:1.16.8'
        testCompile 'junit:junit:4.12'
    }
}

project(':module-core') {
}

project(':module-core-export') {
    // 個々のprojectでのみ必要な依存性
    // 他のprojectの依存性も設定可能であり、eclipseの.classpathファイルにも反映される。
    dependencies {
        compile project(':module-core')
    }
}

project(':module-core-import') {
    dependencies {
        compile project(':module-core')
    }
}

project(':module-export-foo') {
    apply plugin: 'application'
    mainClassName = 'org.tiqwab.example.gradle.multi.BatMainExport'

    dependencies {
        compile project(':module-core-export')
        compile 'org.apache.commons:commons-lang3:3.4'
    }
}

project(':module-import-bar') {
    apply plugin: 'application'
    mainClassName = 'org.tiqwab.example.gradle.multi.BatMainImport'

    dependencies {
        compile project(':module-core-import')
    }
}

// 正常実行確認用
task hello << {
    println "hello"
}

// buildBatタスクで作成されるdirectoryを消去する。
task clean {
    doLast {
        delete 'build'
    }
}

// bat実行用にbuild
task buildBat << {
    // build先のdirectory作成
    // gradle.propertiesで定義しているdot表記のproperties取得は要注意
    def batDir = rootProject.properties['build.bat.dir']
    def baseDir = rootProject.properties['build.base.dir']
    copy {
        from "${baseDir}"
        into "${batDir}"
        exclude '**/*.bat'
        exclude '**/*.sh'
        includeEmptyDirs = true
    }

    // 依存libraryと各modleのcopy
    def libDir = rootProject.properties['build.lib.dir']
    def deps = []
    subprojects.each { p ->
        p.configurations.compile.each { dep ->
            if (!deps.contains(dep)) {
                deps << dep
            }
        }
        def archiveJarPath = p.jar.archivePath
        if (!deps.contains(archiveJarPath)) {
            deps << archiveJarPath 
        }
    }
    copy {
        from files(deps)
        into libDir
    }

    // 各moduleの実行用batファイルのcopy
    def binDir = rootProject.properties['build.bin.dir']
    subprojects.each {p ->
        def scriptsPath = p.buildDir.toPath().resolve('scripts').toFile()
        if (scriptsPath.exists()) {
            copy {
                from scriptsPath
                into binDir
            }
        }
    }
}

